~ SEED_RANDOM(235)

You awaken in a forest,

 * [Look around]
 ->look_around
 * [Close your eyes]
 ->close_eyes
 ==grove
 The same two paths lead off from the clearing.
 
 +[Take the path to {monastery: The Monastery | the right}]
 ->right
 +[Take the path to the {station_name: Station House| left}]
 ->left
 
 ==look_around
 You are standing in a clearing. The light is filtering through the trees, the sun fighting through to hit your face. 
 Two paths lead off from the clearing. 
 +[Take the path to the right]
 ->right
 +[Take the path to the left]
 ->left
 ==close_eyes
 You hear faint birdsong and twigs snapping. Branches rustle and you feel the wind gentle on your skin. //remove?: Though you hear no sounds of people, you cannot be certain that you are not being watched.
*[Follow your ears]
{~-> right | ->left}
*[Follow your intuition]
{~-> right | ->left}


==right
//route to donkey
{stream: You head back towards the Monastery, hearing the stream ahead of you.|You walk deeper into the woods. The ground is cool underfoot, the air has a chill here. {board_secret: You can't think of a more wonderful place. | You think of soft carpet and wood floors and wish you were home.} You hear water ahead of you.}

+[Continue on]
->stream

==stream
{monastery:You reach the stream.|You reach a stream. On the other side you see the forest thinning and can make out the faint shape of a building through the trees.}
+ {not monastery}[Wade through the water]
->wade
+[Jump across]
->monastery

==wade
Your clothes feel heavy in the frigid water, the current tugs at you aggressively.
Out of the corner of your eye, you see something glinting at the bottom of the stream. 
You hear a sound in the woods behind you, almost like an intake of breath.
*[Cross the stream quickly]
->monastery
*[Investigate the glinting]
->shell

==shell
Plunging your hand into the icy water, you fumble around until the cold becomes too much to bear. You open your fist and see a small iridescent shell nestled in your palm. You pocket it.
*Finish crossing the stream
->monastery



==monastery
 {monastery>1: You follow the path to a familiar set of ruins. You have returned to The Monastery.|{board_secret:With an impressive bound, you easily clear the water.} Beyond the stream, the path leads to a set of ruins. Your gaze settles on a crumbled set of arches, and several stone slabs with detailed carvings long eroded into vague impressions. <p>You have found <b>The Monastery</b>.</p>} 

+[Enter the Monastery]
->enter_mon
+ {not board_secret}[Return to the grove]
->grove
+{board_secret}[Hop back to the grove]
->grove

==enter_mon
{approach_donkey or explore_mon: You are standing in the entryway of The Monastery. The old donkey is still happily chewing his grass.| You carefully cross the moss-covered threshold of The Monastery. The ground beyond is blanketed by a tall yellow grass, the horizon is swallowed up again by thick forest. An old donkey is a few yards off, feeding in the grass contentedly.}  

+[Approach the donkey]
->approach_donkey
+[Explore The Monastery]
->explore_mon
+[Return to the grove]
->grove

==approach_donkey

{quiet or confession: |You approach the donkey. Hearing your footsteps, it lifts its head and regards you blankly.} 
{confession and not quiet:The donkey is looking at you. You feel a little judged.}
{not conversation and approach_donkey>3:You feel like you're missing something...Maybe you should try the other path.}
+{quiet<3}[Stay quiet]
->quiet
*[Confess something]
->confession
+[Walk away]
->enter_mon
+{conversation} [Tell a joke]->joke

==quiet
{quiet>1: You and the donkey stand together in a companionable silence.|You and the donkey stand in awkward silence.}
{quiet>2: He is now your friend.}

->approach_donkey

==confession
You tell the donkey that you lied and told your friends that you got your driver's license. You did it on a whim but now it's been years. If any one asks you to drive somewhere you just make an excuse to get out of it, like you've had a drink or think you need glasses. You fear it won't be long before a well-meaning friend stages an intervention (or books you an eye test). <p>You're not sure why you're telling the donkey this.</p>
*[...]
->approach_donkey

//after you've told the existential joke you are sent here if you try and keep joking:
==no_joke
 You think of the station master and her riddle.
+[Return to the grove]
->grove
+[Spend more time in The Monastery]
->enter_mon

==joke
{existential_joke: You don't feel like joking anymore.->no_joke}
The donkey looks at you expectantly.
*[Tell a knock-knock joke]
->knock_knock
*[Tell an existential joke]
->existential_joke

*[Tell a silly joke]
->silly_joke

*Go back
->enter_mon
*{secret} [Tell a dark joke]
->dark_joke

==knock_knock
"Knock Knock
Who's There
Tank.
Tank who?
You’re welcome."

You have to say both parts because the donkey can't speak.
He seems unimpressed.

*[Try a different joke]
->joke
==existential_joke
You ask the donkey if he ever wonders why he's here. If he ever dreams of death. You can't think of a way to make that funny.
*[...]
->existential_joke1
==existential_joke1 
The donkey seems to consider your words. The edges of the forest darken and you become uncomfortably aware of your own mortality. 
*[...]
->existential_joke2
==existential_joke2
You feel a little faint, and reality warps a little. You see marble halls, towering columns. Three brothers on their thrones. You hear the donkey's braying laugh and the sing of metal through air. Blood soaking fine green silk. The sound of a  goblet clattering to the ground.

->no_joke
==silly_joke
You do a committed but amateur impression of Kermit attempting to sing a song about tax fraud. The donkey doesn't quite get it.
*[Try a different joke]
->joke

==dark_joke
"My grandfather said my generation relies too much on the latest technology."
You pause for effect
"I called him a hypocrite and unplugged his life support."
*[Add a <i>badum tss</i>]
->badum
*[Wait]
->laugh
==badum
You do your best impression of a snare drum. You could swear the donkey lets out a little sigh of dissapointment.
*[Apologise for the <i>badum tss</i>]
->laugh
*[Own it]
->laugh
==laugh
 {badum: The donkey decides to overlook your sound effects and lets out a long braying laugh at the joke. | The donkey lets out a long braying laugh.} It echoes oddly around the ruined monastery and makes your teeth hurt. Your hand feels heavy.
 *[Look down]
 ->gun
 ==gun
 You are holding a pistol in your left hand. The donkey is nowhere to be seen.
 *[Return to the entrance of The Monastery]
 ->enter_mon
->DONE

==explore_mon
You wander around The Monastery{. Stone slabs litter the ground, delicate carvings and mosaics eroded by time. A ray of sun breaks through the clouds and catches on one of the slabs. | again. The sun now catches on the shell mosaic {not secret: properly, and the light diffracts around the room, casting stars onto the walls. | and reflects onto that strange message you revealed earlier.}}

*[Inspect the glittering slab]
->slab
*{not more_explore} [Keep exploring]
+{more_explore}[Return to the stained glass window]
->more_explore
+[Turn back]
->enter_mon

==slab
The slab has a pearlescent mosiac on it, pieces of shell forming an ornate spiral winding out from the center. You notice one of the shells is missing.{not shell: You wish you had something you could put there.}
*{shell}[Place the shell from the river in the gap.]
->secret
+[Explore more]
->more_explore

==more_explore
{In a far corner of The Monastery you find the remnants of a stained glass window, still partially intact. As the sparse sunlight illuminates it, you see it depicts three figures in majestic robes. One holds a cup, one a sword, and the final one holds nothing. The glass is shattered where the final figure's head should have been.| You are back in the corner of The Monastery, looking up at the stained glass window.}
+[Return to the entrance of The Monastery]
->enter_mon
*{board_secret}[Follow your ears]
->lapinate_chantry

==secret
You hear the grinding of stone against stone, and notice a section of ruined wall you don't remember seeing before. Inscribed in it are the words "The donkey has a dark sense of humour".
+[Return to the entrance of The Monastery]
->enter_mon
*{not more_explore} [Explore more]
->more_explore

==lapinate_chantry
There is something humming - no - singing to you from behind the stained glass window.
*[Break through]
You smash what remains of the window to pieces and clamber through, the song's urgent beauty distracting you from any cuts.
-You enter a small hidden courtyard. It appears to have been blocked off from the rest of the monastery sometime after its construction.
*[Look around]

You get down onto your haunches and scrabble around in the dirt and grass. 

-(inscription){You find small beads inscribed with spirals, candle stumps, and fragments of wood covered in faded carvings. <i>Something</i> was once worshipped here. |What else?}

*[Inspect the beads] You hold one of the beads up to your eye. They look to be made from bone. They appear slightly ovular in shape and each is tipped with a yellow opal, the colour of which runs through the etched spirals like veins. ->inscription  //fred: I find the last sentence unclear to imagine for some reason , can you think of another way to describe what you mean? 
*[Smell the candles] They are lightly scented with spring flowers. ->inscription
*[Gather the wood together] You manage to connect some of the scraps of wood into the semblance of a whole text - it's a prayer of some kind.

<i>...and so our fecund lord, who bounds through the gloaming... make haste to greet your piteous subjects...we from whom you spring eternal...Lapinite songs uplift you...swiftly dash through warren and briar, bringing with your blessing...anoint our Lagomane as your chosen...use the vessel...bring forth your divine music...keen through the sepulchre...<i>

-Your nose twitches as you read the fragments of an ancient call to an ancient god. Something is buried here. 

*[BURROW]

-You scratch-scratch-scratch at the hard-packed earth. You don't know what you're looking for, but you know it's here. Just as you begin to lose faith, your dirt-caked hands reveal something.

*[UNEARTH]

A figurine of a Hare, carved in the same material as the beads, with yellow opals for eyes. This is what was making the song. 

-You can <b>smell</b> it, and <b>hear</b> it, and <b>taste</b> it. The song of things <b>growing</b>, of things <b>multiplying</b>, of things <b>becoming greater</b> than they are. The song of <b>creeping overgrowth</b> and <b>glut</b> and <b>rot</b>. It sings to you now of <b>change</b>, an <b>incredible change</b>. One already partly underway. 

*[<b>ACCEPT THE SONG</b>]->transformation
*[Reject the song] The hare figurine crumbles away to ash in your hands - leaving only its opal eyes. The bite on your hand throbs sharply. You have a feeling it will never fully heal. 

You should leave this place before something worse happens ->enter_mon

==transformation

The Bone-Hare's eyes glow yellow and the timpanic pounding, the cacophany of rush and dew and fervor, coalesces into a single keening note. Beginning in the wound on your hand, it reverberates through your very being. 
*[<b>CHANGE</b>]
<i>Wound closing - flesh tearing - muscle engorging - hair spreading - tooth elongating - nail sharpening - ear stretching - stature building - divine encapsulating

<i>frog - viper - cicada - ant - butterfly  - alate - larvae - egg - embryo - spawn - paturate</i>

<i>shed - molt - cocoon - pupate - metamorphise -</i> 
**[EMERGE] 

- I <b>am</b> now. I <b>smell</b> now I <b>hear</b> now. Out - out - out - time to <b>run</b>, time to <b>flee</b>. I once was lost. I once was other. I can <b>grow</b> more - must grow more. 

**[ESCAPE]
I <b>hop-hop-leap</b> over the stone walls. I land near a chewing creature. A  malevolent creature. I did not see it before for what it truly was. Like my Lord, but older. Weaker. 
***[RUIN] I <b>twitch-twitch-twitch</b> my nose and a great groaning I hear and I can feel the <b>growth</b> that overtakes the old-stone building. The Laughing Nightmare looks at me in surprise and makes to laugh again, but too late for he is already <b>flesh-become-rotten-become-skin-become-bone</b>. 
***[RUN] <b>Where to-where to- where to?</b>

---I soar into the sky, over <b>rush-raging</b> stream and <b>thump-thump-thump</b> down into the glorious overgrowth. What a fool I was to fear it. Next-

****[TUNNEL] I scurry-rush through the undergrowth. What <b>strength</b> I have now, what skill. I am the greatest conjoining of my form and my lord; enough of me is still capable of what the <b>beast</b> is not.  

----I emerge at a ruined place. Some growth, not much. And that woman again. The vexing-hexing woman who tormented-toyed with me so when I desired leave. I would not wish such a thing now.

*****[TALK]I <b>thump-land</b> on her platform and she emerges to see my glorious state. Her eyes widen. <p>"You shouldn't have done that. Each to their own, I suppose. I assume you won't be needing that train anymore then."</p> She laughs wanly. I tell her of my <b>purpose</b>. She knows there is nothing she can do. Perhaps she will leave. She will leave.
*****[TERRORISE] I beat-beat-beat upon the ground and watch as the building becomes <b>consumed</b> by the creeping earth. She emerges from her crumbling den and stares at me.
I tell her that she should have kept us away better. Her claim on this land is worthless now. I cannot kill her, unnatural entity, bridge to choking-smog barren land.
 The <b>pulse-lurch</b> of the <b>hunt</b> must have her never still. I can make it so.

----- The sun begins to set. I must make a den. Make plans. ->rabbit_ending 

//fred: 1. worried laughing nightmare wont make sense if you havent told the donkey a joke - i think it's fine if things are a little mysterious - the wererabbit now knows more cuz of god stuff I'd imahine, which the player isn't exactly privy to (hence the perspective shift)
//2. Potential lore gap, should stationmaster be affected by the donkey being killed? have suggested here instead that she is a bridge to the modern world which is what makes her difficult to kill as she's stronger + more of a concept. This is a very good point. I think the donkey is not killed permanently, but its physical form has been destroyed. (It would take a god to kill a god, and the wererabbit is not a god, only the vessel for one)
//3. Imo, the below should be deleted and the rabbit ending should finish on "must make a den. make plans". lmk - i think fair point I was honestly just kinda cramming in lore here lol - i think what i was going for is that you're gna try and restart the cult (even if the god has changed from what it once was - ie god of spring to god of rot)

==rabbit_ending
*[WARREN]
-I burrow into that grove where I first found myself before I became. I shall home here. But my Lord is not so strong in this world anymore. Not like he was. He knows that <b>change</b> is in his nature. <b>Rot</b> is life too. And so he can<b> grow</b> again. I can make him <b>grow</b> again. The Lapinate Chantry fell, but I shall not. I shall... revitalise. 
*[REFLECT]
I have done much in a day. I have forgot myself, explored, and not found my old self but taken upon a new one. What luck that an empty vessel such as I would happen upon an emissary of the Bounding One. 

Though I know such things can't be luck. 

->END

==left
{You walk deeper into the woods. Everything is quieter here. Shadows seem to grow from the spaces between leaves, and glancing at them you see something looking back at you.|You walk back into the deep-woods. You cannot help feeling watched again.{pet: Your bite throbs sympathetically. <i>It</i> watches you from the shadows, you know it.{board_secret: You feel strangely comforted by this}}}

*  { not station }         [Reach into the shadows]          ->critter
+ {station and not board_secret} [Make your way swiftly, avoiding any gazes that catch your eye] ->station
* {not station} [Try to ignore it as best as possible]  ->station
+{board_secret} [Dance through the woods on nimble feet] ->station

==critter
You pull back the leaves to reveal an unusually calm rabbit. As it gazes at you with those too-big, too-smart eyes, it seems to make a decision. 
*[Try to pet it]
->pet

*[Continue on] ->station

->station
==pet
You attempt to pet the rabbit. It swiftly bites you on the hand and lopes back into the darkness. Didn't anyone ever tell you not to touch wild animals?

*[Keep following the path] ->station

==station

{station_name: You make your way back to the outside of the old station house. The trees rustle all around you. {board_secret:You can smell them in the darknss of the forest, hear their footsteps in the undergrowth.}| You make your way through the trees, {pet: nursing the weeping bite you now sport on your hand,|trying to put the thought of those large yellow eyes out of your head,} until, without warning, you find yourself standing before an old building.}
+{station_name}[Approach the station house] ->station_house
+{(station_house or grove) and not board_secret}[Go back down the path] You take the path back to the grove you first found yourself in. ->grove
+{board_secret}[Scurry back the way you came] The forest doesn't seem so dark anymore as you take the route back the way you came. 

->grove

* {not station_name} [Inspect the building] A comfortable looking old thing, if slightly overgrown. What you first took for a porch you quickly realise is actually a train platform, complete with an information board and benches.
* {not station_name} [Look around] As you glance around, looking for any signs of civilization that may accompany this strange structure, something in the distance catches your eye; a line that you follow right to your feet. 

- You're standing on railway tracks hidden by leaves and grasses, either end receding into the gloam. ->station_name

==station_name

- <p>You have found <b>The Station House</b>.</p> Maybe there's a way out of the woods after all?
++[Approach the station house] ->station_house
++{not board_secret}[Turn Back] You make your way back to the grove you first found yourself in. ->grove
++{board_secret}[Hop back] With a spring in your step, you skip your way back to the grove you first found yourself in. ->grove


==station_house
{information_board or station_master:You are alone on the platform aside from {station_master: the figure of the station master, still doing who knows what in the waiting room|the frosted glass of the waiting room door, through which you now spy a silhouette,} and, of course, the information board.|You make your way up and onto the platform. You can see the information board more clearly, and just behind it the frosted glass door of a small waiting room.}  

+ {information_board<2}[Inspect the information board] -> information_board
+ {not riddle_answer} [Go through the door] -> station_master
+ {riddle_answer} [Go through the door] -> riddle_answer
+ {riddle_answer} [Go back] -> station

==information_board

{A confusing mess of symbols and lines loop in an odd spiralling pattern, passing through towns that you're sure aren't there, or at least don't exist <i>anymore</i>, and going alongisde a coastline that you can't recognise. | The same board as before with the same information. Confusing, jumbled, and entirely unintelligble. Your head begins to hurt after a while.}

The next train to where you think 'here' is will either arrive in half an hour, tomorrow, or three months from now. Helpful. {pet and information_board>1: ->board_secret}

+[Look away] ->station_house 

==board_secret

You begin to turn away when the pain from the bite on your hand flares up again. Dark veins spiderweb out from the pair of weeping red marks that pulse with sensation.

As you look up from your hand, you notice something on the board that you hadn't before. A smudge that you took to be paart of the map, but can now <i>smell</i> that it must be dirt. How had you not noticed that before?

*[Scrape it off]

You use the fingernails of your non-wounded hand to scrape the dirt off the surface of the map. Underneath it you find the symbol for a temple - or monastery maybe, and a name that means nothing to you.

Yet as you read it, you can't help but feel your heart <i>thump-thump-thumping</i>.
-Perhaps you should look out for the Lapinate Chantry, whatever that may be. 

*[look away] ->station_house

->END

==station_master

You open the door to see a modest waiting room, muffled by a thick layer of dust. One of the back windows is broken and through it the forest has begun to reclaim the space.

A figure stands in the middle of the room, wearing an immaculate double-breasted coat. Her name badge reads "STATION MASTER".

*[Speak to the Station Master]
->conversation
*[Go back]
->station_house

==conversation
You clear your throat.
* [Say hello] 
You begin to introduce yourself, but realise your name has slipped right out of your mind. 
** [Tell her you are lost]
"Good Afternoon, Lost. I'm the station master, how may I be of service?" She replies, unblinking.
***[Correct her] You tell her your name isn't 'Lost', that it's just what you are.
"Oh, of course. I'm sorry."

****[...]
She doesn't look particularly sorry. 
*****[Ask her name]
She pauses. "Station master is fine."
*****[Ask her to help you] 
She cocks her head. 
"You may have to be a little bit more specific. Anything to do with this station, probably yes. However, if you were in dire need of a buttermilk pancake or were bleeding out, I'd probably struggle a little bit.{ critter: That bite of yours does look painful, though I doubt it'll scar.| But I don't think you're in any danger of that happening.}" 
 
*** [Ask how to leave the forest]
"I've long since forgotten the route on foot, but there's certainly a train that could take you to your destination." 
** [Tell her you are looking for the station master]
"That would be me," she taps her name badge with a well-manicured fingernail. "How can I help you?"

- (wabbit_talk) {She smiles but it doesn't quite reach her eyes. You can't tell how old she is. | Her strange smile has fallen in a most discomforting way.}

*{board_secret}[Ask what the Lapinate Chantry is] The stationmaster narrows her eyes. "Why woud you like to know? I have no answers that would satisfy you. I would advise you to explore the matter no further." ->wabbit_talk
*[Ask when the next train is]
*[Ask where you are] You ask where you both are,  suddenly aware of your own ignorance.
"The train station, of course." She finally blinks.
**[Press further] You ask her where the train station is.
"In the forest. You must have seen the trees, surely." You feel like this line of questioning isn't going to lead you anywhere.
***[Ask when the next train is]
- She seems faintly amused. "That depends on where you want to go. We have an information board outside with routes and times."
*[I want to leave the forest]
*{information_board} [I've already been there.] You tell her it was unhelpful.
"I'm sorry to hear that. I'll talk to someone about updating it." 
You don't think she will.  
**[Ask her how to leave the forest]
*{not information_board} [I haven't looked at it yet.] You admit you came straight to talk to her.
"I see. No bother, I know its contents better than most."
**[Ask how to leave the forest]
- "Ah. Trains <i>out</i> of the woods." The stationmaster thinks for a moment. "I could tell you, but..."
*[But?]
Her blank eyes take on a glint of mischief. "Well, I'm rather enjoying having the company... I'd be sad to lose it. What would I gain by helping you leave?"
***[Ask what she wants.]
- "Actually, I might find it in me to help you if you help me first. An old man passed through a short while ago and told me a story that I can't make head or tails of..." She looks at you expectantly.

* [Let her speak]
* [Tell her you don't have time for this] She continues, ignoring you completely. 

- "Let's see if I remember it..." 

*[...]
->riddle_begins

->riddle_begins
==riddle_begins==
- <i>Many years ago, this forest was once the site of an ancient kingdom.</i>

<i>The king, on his deathbed, bequeathed his three sons each equal part of the kingdom and gifted each an item from his imperial regalia.</i>

*[A foolish decision]

- <i>Three thrones were set in place at the royal court, and for a while the brothers ruled peacefully,  side by side.</i>

*[But peace can only last so long...]

- <i>One day, a  man with a donkey entered the court seeking the brothers’ counsel. Each king sat resplendent in his signature colour, one in red, one in black and one in green.</i>

*[Easy enough to tell them apart]

- <i>There are many accounts of what happened that day, but all agree on the following: Firstly, the man spoke in a low voice,</i> 

*[Why was he there?]

- <i>The most cunning king in red leaned back and paused in contemplation. From the left, the black-robed king leapt up and made as if to strike the man. The final brother, in green, adjusted his crown nervously.</i>  

*[What did he say?]

- <i>The donkey at the old man’s side let out a long braying laugh, shattering the tense silence.</i>

*[What made the donkey laugh?]

- <i>At once, the king who had been gifted the imperial sword drew it and ran the brother next to him through. </i>
<i>The remaining king stood wide-eyed, looking over his slain brother’s body at the king with the sword. Unable to believe his own brother capable of such betrayal, he could not imagine the same fate would befall him. </i>

<i>His expression remained fixed in disbelief as his head fell from his body. </i>   

*[Why would brother betray brother?]

- <i>Two clattering sounds rang out, first the goblet, and then the crown. The king with the sword took the crown from where it lay, and lifted his other brother's goblet to toast the man as he led his donkey out of the castle and into the woods.</i>

*[As it always does, it ends with the woods]

- The station master opens her eyes. You don't remember her closing them. 

"And there it is. I'm sure you understand the pickle I'm in. I can't for the life of me figure out which brother did it. He had the sword obviosuly, but other than that it's a mystery. If you could figure out the answer to my little conundrum, I would be more than happy to tell you about your departing train." 

*[Acquiese...begrudgingly]
"Splendid!
*[Ask once more when your train is arriving]
"It shouldn't take that long. I can tell you right now that you have plenty of time before your train arrives to figure out the answer.
*[Ask if you have any choice in the matter]
"I'm not sure I know what you mean.
- -> riddle_answer

==riddle_wrong
~ knows_t = false
~ knows_c = false
~ knows_p = false

"Want to give it another shot?" -> riddle_answer

==riddle_answer

VAR r_colour = ""
VAR k_trait = ""
VAR t_pos = ""

VAR knows_t = false
VAR knows_c = false
VAR knows_p = false


{<> If you think you know now please do share." | <> The station master smiles at you placidly, waiting for you to speak.}
*{gun} [Threaten her]
->gun_end1
+[Ask for some more time to think about it] 
"Of course! I haven't been able to figure it out and I've been at it for who knows how long."

As you move to get away from this strange woman, she offers you some words of advice.

"I've heard that the nearby monastery has a certain equine abbot that may be able to help you. He loves a joke, so I've been told." 

**{not enter_mon} [Equine abbot?]

-> station_house

**{enter_mon} [She must mean the donkey. I better go see it again... and tell it a joke?]

-> station_house

+ {riddle_answer>1} [Ask for more time] "Pity. I shall be waiting with baited breath for your answer."

->station_house

+ [Solve the tale]

{riddle_wrong<4: "Excellent! Please go on." | "Oh good. I'll just..." The station master {riddle_wrong<5:produces a magazine seemingly from nowhere and begins flipping through it. "Don't mind me I'm still listening." You're not sure that she is.|returns to her magazine and idly flips to the next page. "Go on then."}}

++"The king with the sword..."[] you begin, feeling unsure of yourself. "Wore robes of..."
+++"Red.["] 
~ r_colour = "red"
+++"Black.["]
~ r_colour = "black"
~ knows_c = true
+++"Green.["]
~ r_colour = "green"
-- <> He struck his brothers...
++++"Naïvely["]
~ k_trait = "naïve"
++++"Cunningly["]
~ k_trait = "cunning"
++++"Brashly["] 
~ k_trait = "brash"
~ knows_t = true 
---- <>, from the...
+++++"Leftmost["]
~ t_pos = "to the left of"
~ knows_p = true
+++++"Middle["]
~ t_pos = "in between"
+++++"Rightmost["]
~ t_pos = "to the right of"
----- <> throne."

- She intones the following{riddle_wrong<4:, almost in a trance: | without pausing from reading{riddle_wrong<5:: |. She sounds almost bored.}}

"Sat on his throne {t_pos} his brothers, the king in {r_colour} raised his sword above his head and brought it down in two {k_trait} strikes." 

The station master{riddle_wrong<4: pauses to consider this | looks up from her magazine}{knows_t && knows_p && knows_c :... -> the_end | and frowns. "No, that doesn't feel quite right." She taps her chin and looks back at you. -> riddle_wrong }

-> grove

==the_end

*[...]
Her face lights up in a smile.

"Yes, of course. That makes perfect sense. But what did the man say? And why did the donkey laugh?"

She grins at your expression{riddle_wrong>4: before dropping her magazine onto a nearby chair | taking out her pocketwatch} and moving towards you.

"{not approach_donkey and riddle_wrong<4:Somone as ingenious and discerning as youself would know the answer to this, but alas, | Questions for another time, perhaps.} I believe you have a train to catch. It should be arriving any second now. For furture reference it's the half-past train that takes you out of the woods."

You hear a rumbling faintly coming from behind the door.

**[Go outside - to freedom!]

You burst out the door of the waiting room, onto the platfrom and see - 

***[What?]
***[A train, surely]

-...

*[What do I see?]

-You are alone on the platform aside from the figure of the station master, still doing who knows what in the waiting room and, of course, the information board.

*[Inspect the information board] As you move towards the board
*[Go through the door] As you make to head back inside
*[Go back] As you make to leave

- <> the stationmaster pokes her head out of the doorway. 

"Hm. Well, you know how trains are. Should be here shortly. I've never heard of a delay longer than two hours. They often just cancel them in that case."

She smiles unhelpfully. There's only one thing to do now.
*[Wait] You sink down onto the ground and put your head in your hands. You're not sure how long you'll be stuck here.
*[Complain] "Well I can't do anything about it, can, I? I'm just the stationmaster, I don't control the trains." You swear you see her wink.
-"Tell you what, I've got a deck of cards. We can pass the time together."
*[Wait]
-The stationmaster teaches you a complicated card game called 'Usurpation' that requires you to keep the black and red kings as far from each other as possible. You teach her gin rummy.{riddle_wrong<1 and not approach_donkey: You beat her soundly at both and she sulks for a minute or two, clearly not used to losing | She beats you viciously at both}.
*[Wait]
-After a while the stationmaster bustles off to her mysterious lodgings and returns with a tray of iced tea and buttermilk pancakes. You sit and have a lovely chat about birds. 

You can see a dot on the railtracks to your left in the far distance. 
*[Wait]
-You get up to inspect the information board and the stationmaster follows you. She begins to explain how to interpret the map {not approach_donkey:and you find that you can {riddle_wrong<4:quite easily interpret it. Knowing what you know now, you shall have to retrun to the stationhouse for tea and card games again. |somewhat interpret it, though you have to sit down after a while as you head begins to spin and your vision blurs} |{riddle_wrong<4: and you can't begin to interpret it at all. The stationmaster brings you another cup of tea though, which is nice. | and you get so confused that you have to sit down. The stationmaster instead shows you her magazine collection. It's a train magazine, but they do have a swimsuit edition. Interesting.}} 

The dot has grown somewhat bigger.

*[Wait]

-You have a small sleep on one of the - surprsingly comfortable - benches in the waiting room. The stationmaster promises to wake you when your train is close. 

Before you lie down, you see that the dot is nearly double its original size.

*[Nap]

-You feel a rumbling again and wake to the stationmaster telling you that your train is nearly here. 

*[Leap to your feet]

- It's dusk in the forest, and the shadows have grown deeper, though this is nicely offset by the orange underglow of the setting sun. 

Your train is just pulling into the station as you emerge from the waiting room - the dying light plays nicely off of its red caboose. It screeches to a halt and a wood panelled door aligns itself perfectly infront of you. 

There is a pause, one in which no one exits the train, but one can get on it. 

*[Stay] You turn around and find that you cannot bring yourself to get onto the train. ->stay_end
*[Go] You reach for the door and open it, embarking on your erstwhile journey out of the woods. ->go_end

==stay_end

The stationmaster stands behind you.

"One last farewell?"

You explain to her that, for some reason, you don't want to leave.

"I'll be flattered and believe that to be about my company." Her face hides an emotion that you don't think you recognise.

*You wonder if it is.

Whatever the case - be it the serenity of {enter_mon: the monastery| the woods}, the cosiness of the stationhouse, or just the escape from a life outside you can't remember, you know that you wish to stay. For now, at least.

"Luckily for you, I have a spare room. I'll get you settled in and we can wake up bright and early tomorrow with pancakes. I have some jobs that you can do while you're with me."

The stationmaster takes you by the shoulders and leads you away from the edge of the platform as the train pulls away into the setting sun.

-> END

==go_end

You turn around and wave at the stationmaster who waves back. 

"It was lovely having you here. Hopefully I'll see you again soon?"

You agree, though with the caveat that it be under more willing circumstances.

*[Go further into the train]

-Blessedly, the train is fairly empty, and you easily find a carriage to yourself. As you sink down onto the plush upholstery of the seating, you turn to the window and look out onto the platform. 

The stationmaster busies herself clearing away the detritus the pair of you left and only turns as the train begins to pull away. You give her a wave that she returns and soon find yourself speeding through the forest at night. 

*[Dream]

<i>Three children in red and green and black. They fight over something that you can't quite make out. A figure in the corner laughs, a heavy, braying laugh. All is then lost amongst the trees.</i>

You are roused from your slumber from a familiar looking man, who, through your groginess, you realise is the ticketmaster. 

You smile and reach into your pocket before realsing that you never actually bothered to buy a ticket. Bother. 


-> END
==apology==
You're not sure what got into you. She ignores your apology, but says "I see you met the Ass. Don't feel too bad. He has a way of getting into people's heads." <p></p>"Anyway," her eyes glaze over again, "that pesky story..."
<p></p>
->riddle_answer
==gun_end1
You tell the stationmaster that you are sick of these games, and you want her to tell you everything she knows <b>now</b> or else. She looks at you like she sees something you don't, but says nothing.
*[Apologise]
->apology
*[Fall further]
->gun_end2
==gun_end2
You put your hand in your pocket and feel the heavy weight of the gun. You can hear the donkey's laugh on the wind.
*[Fall further]
->gun_end3
*[Turn back]
->too_late

==too_late
{too_late>1: It's too late for that now |You are faintly aware that something is very very wrong. But your hands are moving on their own. }
*[Fall further]
->gun_end3
*[Turn back]
->too_late

==gun_end3
You raise the gun up so it's level with your shoulder. The stationmaster still hasn't spoken. Her expression is unusual; neutral with a hint of resignation. You see stained glass and hear metal clattering, cries of alarm.
*[Shoot]
->gun_end_final1
==gun_end_final1
The force of the shot knocks you both back. You taste iron and hear your heartbeat in your ears. The stationmaster's body is crumpled on the platform, its edges blurring. You feel a sudden shift, and stagger slightly.
*[Look down]
->gun_end_final2
==gun_end_final2
The gun is no longer in your hand. The light glances off of something on your chest 
*[Inspect it]
->gun_end_final3
==gun_end_final3
It's a small metal badge. The text on it reads STATION MASTER.

->END
